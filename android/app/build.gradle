plugins {
    id 'com.android.application'
}

android {
    namespace 'com.ynixon.rotter'
    compileSdk 34

    defaultConfig {
        applicationId "com.ynixon.rotter"
        minSdk 21
        targetSdk 34
        versionCode  project.hasProperty('apkVersionCode') ? project.apkVersionCode.toInteger() : 1
        versionName  project.hasProperty('apkVersionName') ? project.apkVersionName : "1.0"
    }

    signingConfigs {
        debug {
            storeFile     file('../../android/keystore/debug.keystore')
            storePassword 'android'
            keyAlias      'androiddebugkey'
            keyPassword   'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            // Sign release with the persistent debug keystore so APK updates
            // install over each other without requiring a production keystore.
            signingConfig signingConfigs.debug
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    // Custom output filename: rotter-news-<versionName>.apk (no "debug"/"release" suffix)
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "rotter-news-${variant.versionName}.apk"
        }
    }

    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.11.0'
}
